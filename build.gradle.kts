plugins {
    alias(libs.plugins.kover)
    alias(libs.plugins.kotlin) apply false
}

subprojects {
    afterEvaluate {
        tasks.withType<Test> {
            testLogging {
                events("passed", "skipped", "failed")
            }
        }
    }

    pluginManager.withPlugin("maven-publish") {
        apply<JavaPlugin>()

        version = extra["VERSION_NAME"] as String
        group = extra["GROUP"] as String

        configure<JavaPluginExtension> {
            withSourcesJar()
            withJavadocJar()
        }

        configure<PublishingExtension> {
            publications {
                create<MavenPublication>("maven") {
                    from(components["java"])

                    groupId = extra["GROUP"] as String
                    artifactId = extra["POM_ARTIFACT_ID"] as String
                    version = extra["VERSION_NAME"] as String

                    pom {
                        packaging = extra["POM_PACKAGING"] as String

                        name.set(extra["POM_NAME"] as String)
                        description.set(extra["POM_DESCRIPTION"] as String)
                        url.set(extra["POM_URL"] as String)

                        scm {
                            url.set(extra["POM_SCM_URL"] as String)
                            connection.set(extra["POM_SCM_CONNECTION"] as String)
                            developerConnection.set(extra["POM_SCM_DEV_CONNECTION"] as String)
                        }

                        licenses {
                            license {
                                name.set(extra["POM_LICENCE_NAME"] as String)
                                url.set(extra["POM_LICENCE_URL"] as String)
                                distribution.set(extra["POM_LICENCE_DIST"] as String)
                            }
                        }

                        developers {
                            developer {
                                id.set(extra["POM_DEVELOPER_ID"] as String)
                                name.set(extra["POM_DEVELOPER_NAME"] as String)
                            }
                        }

                        organization {
                            name.set(extra["POM_ORGANIZATION_NAME"] as String)
                            url.set(extra["POM_ORGANIZATION_URL"] as String)
                        }
                    }
                }
            }
        }
    }
}
